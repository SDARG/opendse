apply plugin: 'base'
apply plugin: 'maven'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'jacoco'

version = 'SNAPSHOT-' + new Date().format("yyyyMMdd")

def descriptions = [
	'opendse-generator' : 'The test case generator module of OpenDSE',
	'opendse-io' : 'The IO module of OpenDSE',
	'opendse-model' : 'The algorithms module of OpenDSE',
	'opendse-visualization' : 'The visualization module of OpenDSE',
	'opendse-optimization' : 'The optimization module of OpenDSE',
	'opendse-tutorial' : 'The tutorial module of OpenDSE',
	'opendse-realtime' : 'The real-time module of OpenDSE'
]

allprojects {
	group = 'net.sf.opendse'

	repositories {
		mavenCentral()
		mavenLocal()
		maven {
			name "jitpack"
			url 'https://jitpack.io' }
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'maven'
	apply plugin: 'eclipse'
	apply plugin: 'jacoco'

	version = parent.version


	task sourcesJar(type: Jar, dependsOn:classes) {
		from sourceSets.main.allSource
		classifier = 'sources'
	}

	task javadocJar(type: Jar, dependsOn:javadoc) {
		from javadoc.destinationDir
		classifier = 'javadoc'
	}
}

/* for now ignore all javadoc errors in case of Java 8 */
if (JavaVersion.current().isJava8Compatible()) {
	allprojects {
		tasks.withType(Javadoc) {
			options.addStringOption('Xdoclint:none', '-quiet')
		}
	}
}


ext {
	dateISO = new Date().format("yyyy-MM-dd")
}


mainClassName = 'org.opt4j.core.start.Opt4J'

task fatjar(dependsOn: subprojects.jar, type: Jar) {
	destinationDir = new File(project.buildDir, 'fatjar')
	from { configurations.runtime.collect { it.isDirectory() ? it : zipTree(it)
		} }
	manifest {
		attributes 'Main-Class': mainClassName
	}
}

dependencies {
	runtime project(':opendse-model')
	runtime project(':opendse-io')
	runtime project(':opendse-visualization')
	runtime project(':opendse-optimization')
	runtime project(':opendse-realtime')
}

jar {
	manifest {
		attributes 'Main-Class': mainClassName
	}
}

task alldocs(type: Javadoc) {
	title = "OpenDSE $version Project API Documentation"
	destinationDir = new File(project.buildDir, 'docs/javadoc')

	
	options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PUBLIC
	options.links 'https://docs.oracle.com/en/java/javase/12/docs/api/','http://google.github.io/guice/api-docs/4.1/javadoc/'

	subprojects.each {subproject ->
		source subproject.sourceSets.main.java
	}

	subprojects.each { subproject ->
		if( classpath ) {
			classpath += subproject.sourceSets.main.output + subproject.sourceSets.main.compileClasspath
		} else {
			classpath = subproject.sourceSets.main.output + subproject.sourceSets.main.compileClasspath
		}
	}
}

import org.apache.tools.ant.filters.ReplaceTokens

task copyWebsite(type: Copy, dependsOn: getTasksByName('copyWebsite', true)){
	from('src/main/website/') {
		include '**/*.html'
		filter(ReplaceTokens, tokens: [version : project.version, date : project.dateISO])
	}
	from('src/main/website/') {
		exclude '**/*.html'
	}
	subprojects.each { subproject ->
		from new File(subproject.buildDir, 'website')
	}
	into 'build/website/'
}

task copyJavadoc(type: Copy, dependsOn: alldocs){
	from new File(project.buildDir, 'docs/javadoc')
	into 'build/website/javadoc/'+version
}

task website(dependsOn: [copyWebsite, copyJavadoc]) {}


task jacocoRootReport(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
	dependsOn = subprojects.test
	getAdditionalSourceDirs().setFrom(subprojects.sourceSets.main.allSource.srcDirs)
	getSourceDirectories().setFrom(subprojects.sourceSets.main.allSource.srcDirs)
	getClassDirectories().setFrom(subprojects.sourceSets.main.output)
	getExecutionData().setFrom(subprojects.jacocoTestReport.executionData)
	getExecutionData().setFrom(executionData.findAll {
		it.exists()
	})
	reports {
		html.enabled true
		html.destination(file("${buildDir}/reports/jacoco"))
		xml.enabled = true
		xml.destination(file("${buildDir}/reports/jacoco/rootReport.xml"))
		csv.enabled = false
	}
	onlyIf = {
		true
	}
	afterEvaluate {
		getClassDirectories().setFrom(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
				'ptolemy/**',
				'org/opt4j/tutorial/**',
				'org/opt4j/viewer/**',
				'org/opt4j/core/config/visualization/**'
			])
		})
	}
}


